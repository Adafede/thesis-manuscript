### SI 3 Wikidata SPARQL Queries {.page_break_before}

#### Query 1 - *Arabidopsis thaliana*

This query answers to the following question:

```BASH
What are the compounds found in Mouse-ear cress (Arabidopsis thaliana)?
```

Link: [https://w.wiki/3HMX](https://w.wiki/3HMX)
```SQL
# What are the compounds found in Mouse-ear cress (Arabidopsis thaliana)?
SELECT DISTINCT ?structure ?structureLabel ?structure_inchikey WHERE {
  ?structure wdt:P235 ?structure_inchikey; # get the inchikey
    p:P703 ?statement.                     # statement found in taxon
  ?statement ps:P703 wd:Q158695;           # Wikidata identifier of your taxa of interest (here Arabidopsis thaliana). 
                                           # You can remove the Qxxxxxx and hit Ctrl+space, type the first letters and it should autocomplete
    prov:wasDerivedFrom ?ref.              # get results with a reference
  ?ref pr:P248 ?art.                       # get the reference ID
  ?art wdt:P356 ?art_doi.                  # get the reference DOI
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
```

#### Query 2 - *β*-sitosterol {.page_break_before}

This query answers to the following question:

```BASH
Which organisms are known to contain β-sitosterol?
```

Link: [https://w.wiki/3HLy](https://w.wiki/3HLy)
```SQL
# Which organisms are known to contain β-sitosterol?
SELECT DISTINCT ?taxon ?taxonName WHERE {
  {
    wd:Q121802 p:P703 ?stmt.          # limits the search to β-sitosterol
    ?stmt ps:P703 ?taxon.             # found in taxon
    {
      ?stmt prov:wasDerivedFrom ?ref.
      ?ref pr:P248 ?art.              # stated in 
      ?art wdt:P356 ?art_doi.         # DOI of the reference
    }
  }
  ?taxon wdt:P225 ?taxonName.         # scientific name of the taxon
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
```

#### Query 3 - *β*-sitosterol stereoisomers {.page_break_before}

This query answers to the following question:

```BASH
Which organisms are known to contain stereoisomers of β-sitosterol?
```

Link: [https://w.wiki/3Jgs](https://w.wiki/3Jgs)
```SQL
# Which organisms are known to contain stereoisomers of β-sitosterol?
SELECT ?compound ?compoundLabel ?InChIKey ?taxonname 
WITH {
  SELECT ?compound ?InChIKey WHERE {
    wd:Q121802 wdt:P235 ?queryKey .         # β-sitosterol
    ?compound wdt:P235 ?InChIKey .          # get the inchikey of β-sitosterol
    FILTER (regex(str(?InChIKey), concat("^", substr($queryKey,1,14), "-"))) # results containing the first 14 character of the inchikey of β-sitosterol
    FILTER ( ?InChIKey != ?queryKey )       # but not containing the inchikey of β-sitosterol
  }
} AS %compounds
WHERE {
  INCLUDE %compounds
  ?compound wdt:P703/wdt:P225 ?taxonname .  # found in taxon / taxon scientific name
  ?compound rdfs:label ?compoundLabel.      # compound name
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
ORDER BY ASC(?InChIKey)                     # sort by inchikey
```

#### Query 4 - Pigments {.page_break_before}

This query answers to the following question:

```BASH
Which pigments are found in which taxa, according to which reference?
```

Link: [https://w.wiki/3H3o](https://w.wiki/3H3o)
```SQL
# Which pigments are found in which taxa, according to which reference?
# special thanks goes to Layla Michán for updating this information!
SELECT DISTINCT ?compound ?compoundLabel ?taxon ?taxonname ?DOI 
WITH {
  SELECT ?compound WHERE {
    ?compound wdt:P31*/wdt:P279* wd:Q161179.  # get pigments
  }
} AS %compounds
WITH {
  SELECT ?compound ?P703statement WHERE {
    INCLUDE %compounds
            ?compound p:P703 ?P703statement.  # check for "found in taxon" statements
  }
} AS %P703statement
WITH {
  SELECT ?compound ?taxon ?DOI WHERE {
    INCLUDE %P703statement
            ?P703statement ps:P703 ?taxon ;   # get the respective taxa
            prov:wasDerivedFrom / pr:P248 [   # get the reference supporting that statement
              wdt:P356 ?DOI                   # get the DOI for the reference
            ] .
  }
} AS %taxa
WHERE {
  {
    INCLUDE %taxa

            ?taxon wdt:P225 ?taxonname .      # get the taxon name
  }
  ?compound rdfs:label ?compoundLabel .       # get compound labels
  FILTER (LANG(?compoundLabel) = "en") .      # filter for English
}
ORDER BY ASC(?compoundLabel)
LIMIT 10000
```

#### Query 5 - Sister taxon compounds {.page_break_before}

This query answers to the following question:

```BASH
What are examples of organisms where compounds were found in an organism sharing the same parent taxon, but not the organism itself?
```


Link: [https://w.wiki/3HM6](https://w.wiki/3HM6)
```SQL
# What are examples of organisms where compounds were found in an organism sharing the same parent taxon, but not the organism itself? 
SELECT DISTINCT ?compound ?compoundLabel ?taxonname_with_compound ?taxonname_without_compound ?parent_taxon WITH { 
  SELECT DISTINCT ?compound ?taxon_with_compound ?parent_taxon 
  WHERE {
    ?compound wdt:P235 ?inchikey.
    SERVICE bd:sample { ?compound wdt:P703 ?taxon_with_compound . bd:serviceParam bd:sample.limit 1000 }   
    ?taxon_with_compound wdt:P171 ?parent_taxon .
  }
} AS %taxon_with_compound 
WITH
{ 
  SELECT DISTINCT ?taxon_without_compound ?parent_taxon ?compound 
  WHERE {
    INCLUDE %taxon_with_compound
    ?taxon_without_compound wdt:P171 ?parent_taxon .
    FILTER (?taxon_with_compound != ?taxon_without_compound)
  }
} AS %taxon2 
WHERE {
  INCLUDE %taxon_with_compound
  INCLUDE %taxon2
  FILTER NOT EXISTS {  ?compound wdt:P703 ?taxon_without_compound .}
  ?taxon_with_compound wdt:P225 ?taxonname_with_compound .
  ?taxon_without_compound wdt:P225 ?taxonname_without_compound .
  ?compound rdfs:label ?compoundLabel.
  FILTER(LANG(?compoundLabel) = "en").
}
```

#### Query 6 - *Zephyranthes* sister taxon compounds {.page_break_before}

This query answers to the following question:

```BASH
Which Zephyranthes spp. lack compounds known from ≥ 2 other Zephyranthes?
```

Link: [https://w.wiki/3HJf](https://w.wiki/3HJf)
```SQL
# Which Zephyranthes spp. lack compounds known from ≥ 2 other Zephyranthes? 
PREFIX target: <http://www.wikidata.org/entity/Q191364> # Zephyranthes
SELECT DISTINCT ?compound ?compoundLabel ?taxon_with_compound ?another_taxon_with_compound ?taxon_without_compound WITH { 
  SELECT DISTINCT ?compound ?taxon_YES_1 ?taxon_YES_2 
  WHERE {
    ?compound wdt:P703 ?taxon_YES_1 .
    ?compound wdt:P703 ?taxon_YES_2 .
    ?taxon_YES_1 wdt:P171 target: .
    ?taxon_YES_2 wdt:P171 target: .
    FILTER (?taxon_YES_2 != ?taxon_YES_1)
  }
} AS %taxa_with_compound 
WITH
{ 
  SELECT DISTINCT ?taxon_NO ?compound 
  WHERE {
    INCLUDE %taxa_with_compound
    ?taxon_NO wdt:P171 target: .
    FILTER (?taxon_YES_1 != ?taxon_NO)
  }
} AS %taxon_without_compond 
WHERE {
  INCLUDE %taxa_with_compound
  INCLUDE %taxon_without_compond
  FILTER NOT EXISTS { ?compound wdt:P703 ?taxon_NO .}
  VALUES ?classes {
    wd:Q11173
    wd:Q59199015
  }
  ?taxon_YES_1 wdt:P225 ?taxon_with_compound .
  ?taxon_YES_2 wdt:P225 ?another_taxon_with_compound .
  ?taxon_NO wdt:P225 ?taxon_without_compound .
  ?compound wdt:P31*/wdt:P279* ?classes .
  ?compound rdfs:label ?compoundLabel.
  FILTER(LANG(?compoundLabel) = "en").
}
```

#### Query 7 - Antibiotic-like compounds {.page_break_before}

This query answers to the following question:

```BASH
How many compounds are structurally similar to compounds labeled as antibiotics? Results are grouped by the parent taxon of the organism they were found in.
```

Link: [https://w.wiki/3HMA](https://w.wiki/3HMA)
```SQL
# How many compounds are structurally similar to compounds labeled as antibiotics? 
# Results are grouped by the parent taxon of the organism they were found in.
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#> # prefixes needed for structural similarity search
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>
SELECT ?parent_taxon ?parent_taxon_name (COUNT(DISTINCT ?compound) AS ?count) WHERE {
  SERVICE idsm:wikidata {
    SERVICE <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
      ?antibiotic ((wdt:P279*)/wdt:P2868/wdt:P486) "D000900"; # get antibiotics
        wdt:P233 ?smiles.                                     # get SMILES
    }
    ?compound sachem:similarCompoundSearch _:b40.
    _:b40 sachem:query ?smiles;
      sachem:cutoff "0.9"^^xsd:double.                        # similarity cut-off at 0.9 similarity
  }
  hint:Prior hint:runFirst "true"^^xsd:boolean.
  ?compound wdt:P703 ?taxon.                                  # found in taxon
  ?taxon wdt:P171 ?parent_taxon.                              # get the parent taxon 
  OPTIONAL { ?parent_taxon wdt:P225 ?parent_taxon_name. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?parent_taxon ?parent_taxon_name                     # count per parent taxon 
ORDER BY DESC (?count)
```

#### Query 8 - Triples {.page_break_before}

This query answers to the following question:

```BASH
Which are the available referenced structure-organism pairs? (example limited to 1000 results)
```

Link: [https://w.wiki/3JpE](https://w.wiki/3JpE)
```SQL
SELECT DISTINCT ?structure ?structure_inchikey ?taxon ?taxon_name ?reference ?reference_doi WHERE {
  ?structure wdt:P235 ?structure_inchikey;       # get the inchikey
    p:P703[                                      # statement found in taxon
     ps:P703 ?taxon;                             # get the taxon
     (prov:wasDerivedFrom/pr:P248) ?reference ]. # get the reference
  ?taxon wdt:P225 ?taxon_name.                   # get the taxon scientific name
  ?reference wdt:P356 ?reference_doi.            # get the reference DOI
}
LIMIT 1000
```

#### Query 9 - Indolic scaffold {.page_break_before}

This query answers to the following question:

```BASH
Which organisms contain indolic scaffolds? Count occurrences, group and order the results by the parent taxon.
```

Link: [https://w.wiki/3HMD](https://w.wiki/3HMD)
```SQL
# Which organisms contain indolic scaffold? Group and order the results by the parent taxon.
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#> # prefixes needed for structural similarity search
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>

SELECT ?parent_taxon ?parent_taxon_name (COUNT(DISTINCT ?compound) AS ?count)  WHERE {
  SERVICE idsm:wikidata {
    ?compound sachem:substructureSearch
              [ sachem:query "NCCC1=CNC2=C1C=CC=C2" ] # indolic scaffold
  }
  hint:Prior hint:runFirst true.                      # hint to evaluate the idsm service first
  ?compound p:P703 ?statement;                        # found in taxon
            wdt:P235 ?inchikey.                       # get the inchikey
  ?statement ps:P703 ?taxon.                          # get the taxon
  ?taxon wdt:P171 ?parent_taxon.                      # get the parent taxon
  ?parent_taxon wdt:P225 ?parent_taxon_name.          # get the parent taxon name
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?parent_taxon ?parent_taxon_name
ORDER BY DESC (?count)
```

#### Query 10 - Senior NP chemists {.page_break_before}

This query answers to the following question:

```BASH
How many structure-organism pairs have been referenced by certain authors? (Here, two senior natural products chemists and co-authors of this paper are compared to the late Ferdinand Bohlmann).
```

Link: [https://w.wiki/3HML](https://w.wiki/3HML)
```SQL
# How many structure-organism pairs have been referenced by certain authors? 
# Here, two senior natural products chemists are compared to the late Ferdinand Bohlmann
#defaultView:BarChart
SELECT ?authors_namesLabel (COUNT(DISTINCT(?compound)) AS ?count) WHERE {
  ?compound p:P703 ?stmt.           # statement found in taxon
    ?stmt prov:wasDerivedFrom ?ref. # get the reference
    ?ref pr:P248 ?art.              # stated in
  VALUES ?authors_names {
    wd:Q56084663                    # JLW
    wd:Q40259636                    # GFP
    wd:Q1405133                     # A german chemist of the 20th century ... Ferdinand Bohlmann
  }
  ?art wdt:P50 ?authors_names.      # limit to references containing the author names
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?authors_namesLabel
ORDER BY DESC (?count)
```
